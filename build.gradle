plugins {
    id 'java'
    id 'jacoco'
    id 'pmd'
    id 'org.springframework.boot' version '3.5.10'
    id 'io.spring.dependency-management' version '1.1.7'
    id "org.sonarqube" version "5.0.0.4638"
}

group = 'id.ac.ui.cd.advprog'
version = '0.0.1-SNAPSHOT'
description = 'eshop'

def seleniumJavaVersion = "4.14.1"
def seleniumJupiterVersion = "5.0.1"
def webDriverManagerVersion = "5.6.3"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

sonarqube {
    properties {
        property "sonar.projectKey", "B-Iqbal-Rafi-Nuryana-2406417462_Modul-2---CI-CD-DevOps"
        property "sonar.organization", "b-iqbal-rafi-nuryana-2406417462"
        property "sonar.projectName", "Modul-2---CI-CD-DevOps"
        property "sonar.host.url", "https://sonarcloud.io"
        property "sonar.qualitygate.wait", "true"
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
    testImplementation("org.seleniumhq.selenium:selenium-java:$seleniumJavaVersion")
    testImplementation("io.github.bonigarcia:selenium-jupiter:$seleniumJupiterVersion")
    testImplementation("io.github.bonigarcia:webdrivermanager:$webDriverManagerVersion")
}

tasks.register("unitTest", Test){
    description = "Run unit tests"
    group = "verification"

    filter {
        excludeTestsMatching("*FunctionalTest")
    }
}

tasks.register("functionalTest", Test){
    description = "Run functional tests"
    group = "verification"

    filter {
        includeTestsMatching("*FunctionalTest")
    }
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.test {
    filter {
        excludeTestsMatching("*FunctionalTest")
    }

finalizedBy(tasks.jacocoTestReport)
}

tasks.jacocoTestReport{
    dependsOn(tasks.test)
}

pmd {
    toolVersion = '7.21.0'
    ruleSetFiles = files("ruleset.xml")
    ruleSets = []
    consoleOutput = true
}

tasks.pmdTest.enabled = false


